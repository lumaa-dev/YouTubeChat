plugins {
    id "org.cadixdev.licenser"
}

repositories {
    flatDir {
        dirs "libs"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"

    implementation(group: "com.google.apis", name: "google-api-services-youtube", version: "v3-rev20210601-1.31.0") {
        exclude(group: "com.google.guava")
        exclude(group: "com.google.code.gson")
    }
    implementation(group: "com.google.oauth-client", name: "google-oauth-client-jetty", version: "1.31.5") {
        exclude(group: "com.google.guava")
    }
    implementation(group: "com.google.api-client", name: "google-api-client", version: "1.31.5") {
        exclude(group: "com.google.guava")
        exclude(group: "com.google.code.gson")
    }
    implementation(group: "com.google.http-client", name: "google-http-client", version: "1.31.0") {
        exclude(group: "com.google.guava")
    }
    implementation(group: "com.google.http-client", name: "google-http-client-jackson2", version: "1.31.0") {
        exclude(group: "com.google.guava")
    }

    String lib = ":stevekung-lib-fabric"

    if (!file("../fabric/libs/stevekung-lib-fabric.jar").exists()) {
        lib = "curse.maven:stevekungs-lib-296019:${rootProject.stevekung_lib_fabric}"
        println("Use CurseForge artifact")
    } else {
        println("Use existing artifact")
    }
    modCompileOnly(lib)
}

license {
    header = rootProject.file("LICENSE-header.txt")
    include "**/*.java"
}

architectury {
    common()
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
} 