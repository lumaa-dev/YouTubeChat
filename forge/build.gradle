plugins {
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    clientOnlyMinecraftJar()

    launches {
        if (project.hasProperty("username") && project.hasProperty("uuid")) {
            client {
                arg "--username", "${username}", "--uuid", "${uuid}"
            }
        }
    }
}

configurations {
    common
    shadowCommon
    ytDep
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    ytDep.exclude group: "org.apache.httpcomponents"
    ytDep.exclude group: "commons-logging"
    ytDep.exclude group: "commons-codec"
    ytDep.exclude group: "com.google.code.findbugs"
    ytDep.exclude group: "com.google.errorprone"
    ytDep.exclude group: "com.google.j2objc"
    ytDep.exclude group: "org.checkerframework"
}

dependencies {
    forge "net.minecraftforge:forge:${mc_version}-${forge_version}"

    modImplementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
    modRuntimeOnly "curse.maven:lazy-dfu-forge-460819:3831644"
    modRuntimeOnly "curse.maven:configured-457570:${configured_version}"
    modRuntimeOnly "curse.maven:catalogue-459701:${catalogue_version}"

    forgeDependencies(implementation(ytDep(group: "com.google.apis", name: "google-api-services-youtube", version: project.api_services_youtube))) {
        exclude group: "com.google.guava"
        exclude group: "com.google.code.gson"
    }
    forgeDependencies(implementation(ytDep(group: "com.google.oauth-client", name: "google-oauth-client-jetty", version: project.oauth_client_jetty))) {
        exclude group: "com.google.guava"
        exclude group: "com.google.code.gson"
    }
    forgeDependencies(implementation(ytDep(group: "com.google.api-client", name: "google-api-client", version: project.api_client))) {
        exclude group: "com.google.guava"
        exclude group: "com.google.code.gson"
    }
    forgeDependencies(implementation(ytDep(group: "com.google.http-client", name: "google-http-client", version: project.http_client))) {
        exclude group: "com.google.guava"
        exclude group: "com.google.code.gson"
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
    inputs.property "version", rootProject.mod_version

    filesMatching("META-INF/mods.toml") {
        expand "version": rootProject.mod_version
    }

    doLast {
        project(":common").minifyJson(outputs)
    }
}

license {
    header = rootProject.file("LICENSE-header.txt")
    include "**/*.java"
}

String fileName = "${archives_base_name}-mc${mc_version}"

shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.ytDep]

    relocate "com.google.api", "com.stevekung.ytc.relocate.com.google.api"
    relocate "io", "com.stevekung.ytc.relocate.io"

    exclude "META-INF/maven/**"
    exclude "META-INF/proguard/**"
    exclude "youtube.v3.json"

    archiveClassifier = "dev"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

def uploadDebug = true

curseforge {
    apiKey = project.hasProperty("curseforge_api_token") ? project.curseforge_api_token : ""
    project {
        id = "302916"
        changelog = rootProject.file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion "Forge"
        addGameVersion "Java 17"
        addGameVersion project.mc_version

        mainArtifact(remapJar.archiveFile) {
            it.displayName = "[Forge] " + fileName + "-v${mod_version}"
        }
        relations {
            it.requiredDependency "cloth-config"
            it.optionalDependency "configured"
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
        addArtifact shadowJar
        addArtifact remapSourcesJar
    }
    options {
        debug uploadDebug
        forgeGradleIntegration = false
    }
}

modrinth {
    debugMode = uploadDebug
    token = project.hasProperty("modrinth_api_token") ? project.modrinth_api_token : ""
    projectId = "youtube-chat"
    changelog = rootProject.file("CHANGE_LOG.md").text
    versionName = "[Forge] YouTube Chat " + "v${mod_version}" + " for Minecraft " + project.mc_version
    versionNumber = project.mod_version + "-forge"
    versionType = "release"
    uploadFile = remapJar
    additionalFiles = [shadowJar, remapSourcesJar]
    gameVersions = ["${mc_version}"]
    loaders = ["forge"]
    dependencies {
        required.project "cloth-config"
        // optional.project "configured" TODO Enable when "Configured" mod is on Modrinth, Thank you Emmaffle!
    }
}